---
title: log_reduce_predict_fl() - Azure Data Explorer
description: This article describes the log_reduce_predict_fl() user-defined function in Azure Data Explorer.
ms.reviewer: adieldar
ms.topic: reference
ms.date: 24/10/2022
---
# log_reduce_predict_fl()

The function `log_reduce_predict_fl()` parses semi structured textual columns, such as log lines, and for each line it matches the respective pattern from a pre-trained model or reports an anomaly if no matching pattern was found. The function's' output is similar to [log_reduce_fl()](log-reduce-fl.md), though the patterns are retrieved from a pre-trained model that was generated by [log_reduce_train_fl()](log-reduce-train-fl.md).

> [!NOTE]
> * `log_reduce_predict_fl()` is a [UDF (user-defined function)](../query/functions/user-defined-functions.md). For more information, see [usage](#usage).
> * This function contains inline Python and requires [enabling the python() plugin](../query/pythonplugin.md#enable-the-plugin) on the cluster.

## Syntax

`T | invoke log_reduce_predict_fl(`*models_tbl*`,` *model_name*`,` *reduce_col*`,` *anomaly_str*`)`

## Arguments

| Name | Type | Required | Description |
|--|--|--|--|
| *models_tbl* | table | &check; | A table  containing models generated by [log_reduce_train_fl()](log-reduce-train-fl.md). The table's schema should be (name:string, timestamp: datetime, model:string).  |
| *model_name* | string | &check; | The name of the model that will be retrieved from *models_tbl*. If the table contains few models matching the model name, the latest one will be used. |
| *reduce_col* | string | &check; | The name of the string column the function is applied to. |
| *anomaly_str* | string | | This string will be output for lines that have no matched pattern in the model. Default value is "ANOMALY". |

## Usage

`log_reduce_predict_fl()` is a user-defined [tabular function](../query/functions/user-defined-functions.md#tabular-function) to be applied using the [invoke operator](../query/invokeoperator.md). You can either embed its code in your query, or install it in your database. There are two usage options: ad hoc and persistent usage. See the below tabs for examples.

# [Ad hoc](#tab/adhoc)

For ad hoc usage, embed the code using the [let statement](../query/letstatement.md). No permission is required.

~~~kusto
let log_reduce_predict_fl=(tbl:(*), models_tbl: (name:string, timestamp: datetime, model:string), 
                      model_name:string, reduce_col:string, anomaly_str: string = 'ANOMALY')
{
    let model_str = toscalar(models_tbl | where name == model_name | top 1 by timestamp desc | project model);
    let kwargs = bag_pack('logs_col', reduce_col, 'output_patterns_col', 'LogReduce','output_parameters_col', '', 
                          'model', model_str, 'anomaly_str', anomaly_str, 'output_type', 'summary');
    let code = ```if 1:
        from sandbox_utils import Zipackage
        Zipackage.install('LogReduceFilter.zip')
        from LogReduceFilter import LogReducePredict
        result = LogReducePredict.log_reduce_predict(df, kargs)
    ```;
    tbl
    | evaluate hint.distribution=per_node python(typeof(Count:int, LogReduce:string, example:string), code, kwargs, external_artifacts =
    bag_pack('LogReduceFilter.zip', 'https://adiwesteurope.blob.core.windows.net/python-we/LogReduceFilter/LogReduceFilter-1.0.1.zip'))
}
;
// Predict patterns for 1000 entries from the HDFS_log, based on a model that was trained on 100K entries. 
HDFS_log
| take 1000
| invoke log_reduce_predict_fl(models_tbl=ML_models, model_name="HDFS_100k_patterns", reduce_col="data")
~~~

# [Persistent](#tab/persistent)

For persistent usage, use [`.create function`](../management/create-function.md). Creating a function requires [database user permission](../management/access-control/role-based-authorization.md).

### One-time installation

~~~kusto
.create-or-alter function with (folder = 'Packages\\Text', docstring = 'Apply a trained model to find common patterns in textual logs, output a summary table')
log_reduce_predict_fl(tbl:(*), models_tbl: (name:string, timestamp: datetime, model:string), 
                      model_name:string, reduce_col:string, anomaly_str: string = 'ANOMALY')
{
    let model_str = toscalar(models_tbl | where name == model_name | top 1 by timestamp desc | project model);
    let kwargs = bag_pack('logs_col', reduce_col, 'output_patterns_col', 'LogReduce','output_parameters_col', '', 
                          'model', model_str, 'anomaly_str', anomaly_str, 'output_type', 'summary');
    let code = ```if 1:
        from sandbox_utils import Zipackage
        Zipackage.install('LogReduceFilter.zip')
        from LogReduceFilter import LogReducePredict
        result = LogReducePredict.log_reduce_predict(df, kargs)
    ```;
    tbl
    | evaluate hint.distribution=per_node python(typeof(Count:int, LogReduce:string, example:string), code, kwargs, external_artifacts =
    bag_pack('LogReduceFilter.zip', 'https://adiwesteurope.blob.core.windows.net/python-we/LogReduceFilter/LogReduceFilter-1.0.1.zip'))
}
~~~

### Usage

```kusto
// Predict patterns for 1000 entries from the HDFS_log, based on a model that was trained on 100K entries. 
HDFS_log
| take 1000
| invoke log_reduce_predict_fl(models_tbl=ML_models, model_name="HDFS_100k_patterns", reduce_col="data")
```

---

Result:

```kusto
Count	LogReduce	example
239	081110 <NUM> <NUM> INFO dfs.DataNode$DataXceiver: Receiving block blk_<NUM> src: <IP> dest: <IP>	081110 215858 15494 INFO dfs.DataNode$DataXceiver: Receiving block blk_-7037346755429293022 src: /10.251.43.21:45933 dest: /10.251.43.21:50010
231	081110 <NUM> <NUM> INFO dfs.DataNode$PacketResponder: Received block blk_<NUM> of size <NUM> from <IP>	081110 215858 15485 INFO dfs.DataNode$PacketResponder: Received block blk_5080254298708411681 of size 67108864 from /10.251.43.21
230	081110 <NUM> <NUM> INFO dfs.DataNode$PacketResponder: PacketResponder <NUM> for block blk_<NUM> terminating	081110 215858 15496 INFO dfs.DataNode$PacketResponder: PacketResponder 2 for block blk_-7746692545918257727 terminating
218	081110 <NUM> <NUM> INFO dfs.FSNamesystem: BLOCK* NameSystem.addStoredBlock: blockMap updated: <IP> is added to blk_<NUM> size <NUM>	081110 215858 27 INFO dfs.FSNamesystem: BLOCK* NameSystem.addStoredBlock: blockMap updated: 10.250.11.85:50010 is added to blk_5080254298708411681 size 67108864
79	081110 <NUM> <NUM> INFO dfs.FSNamesystem: BLOCK* NameSystem.allocateBlock: <*>. <*>	081110 215858 26 INFO dfs.FSNamesystem: BLOCK* NameSystem.allocateBlock: /user/root/rand3/_temporary/_task_200811101024_0005_m_001805_0/part-01805. blk_-7037346755429293022
3	081110 <NUM> <NUM> INFO dfs.DataBlockScanner: Verification succeeded for <*>	081110 215859 13 INFO dfs.DataBlockScanner: Verification succeeded for blk_-7244926816084627474
```
