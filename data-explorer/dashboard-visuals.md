---
title: Dashboard-specific visuals
description: Visualizations available in Azure Data Explorer web UI or dashboards
ms.reviewer: gabil
ms.topic: how-to
ms.date: 08/02/2023
---
# Dashboard-specific visuals

The following visualizations are available only in Azure Data Explorer dashboards or [web UI](add-query-visualization.md). For visualizations available within the render operator, see [render operator](kusto/query/renderoperator.md). To learn how to customize visuals, see [Customize Azure Data Explorer dashboard visuals](dashboard-customize-visuals.md)

For general information on dashboards in Azure Data Explorer, see [Visualize data with Azure Data Explorer dashboards](azure-data-explorer-dashboards.md).

## Funnel chart

A funnel chart visualizes a linear process that has sequential, connected stages. Each funnel stage represents a percentage of the total. So, in most cases, a funnel chart is shaped like a funnel, with the first stage being the largest, and each subsequent stage smaller than its predecessor.

The following example uses data from the `ContosoSales` database from the publicly available [help cluster](https://dataexplorer.azure.com/clusters/help) to show the number of sales for washers and dryers in Canada out of the total sum of sales in 2007.

```kusto

let interestingSales = SalesTable
| where DateKey between (datetime(2007) .. datetime(2008))
| join kind=inner Products on ProductKey;
let totalSales = interestingSales
| summarize sum(SalesAmount)
| extend Name="Total";
//totalSales
let homeAppliancesSales = interestingSales
| where ProductCategoryName == "Home Appliances"
| summarize sum(SalesAmount)
| extend Name="Home Appliances";
//homeAppliancesSales
let washersAndDryersSales = interestingSales
| where ProductCategoryName == "Home Appliances"
| where ProductSubcategoryName == "Washers & Dryers"
| summarize sum(SalesAmount)
| extend Name="Washers & Dryers";
//washersAndDryersSales
let canadaSales = interestingSales
| where ProductCategoryName == "Home Appliances"
| where ProductSubcategoryName == "Washers & Dryers"
| where Country == "Canada"
| summarize sum(SalesAmount)
| extend Name="Canada";
//canadaSales
totalSales
| union homeAppliancesSales
| union washersAndDryersSales
| union canadaSales
| project Name, SalesAmount=sum_SalesAmount
| sort by SalesAmount desc
```

:::image type="content" source="media/dashboard-customize-visuals/funnel.png" alt-text="Screenshot of Dashboard results pane showing a funnel chart visualization of the example query.":::

## Heatmap

A heatmap shows values for a main variable of interest across two axis variables as a grid of colored squares.

To render a heatmap, the query must generate a table with three columns. The columns that will be used for x and y values must be in the `string` format, and the data used for the *value* field must be numeric. 

> [!NOTE]
> We recommend specifying each data field, instead of letting the tool infer the data source.

```kusto
nyc_taxi
| where passenger_count < 10
| summarize avg(tip_amount) by vendor_id, tostring(passenger_count)
```

:::image type="content" source="media/dashboard-customize-visuals/heatmap.png" alt-text="Screenshot of a heatmap visual from the Azure Data Explorer dashboard.":::

## Plotly (preview)

To render a Plotly visual, the query must generate a table with a single string cell containing [Plotly JSON](https://plotly.com/chart-studio-help/json-chart-schema/). This Plotly JSON string can be generated by one of the following methods:

* Dynamically create the string in Python using the [Plotly package](https://plotly.com/python/getting-started/). This process uses the [python() plugin](kusto/query/pythonplugin.md).
* Retrieve the string from a table that stores pre-cooked Plotly JSON templates. Update the required data fields using KQL string manipulation functions.

The following KQL query uses inline Python to create the 3D scatter chart:

~~~kusto
OccupancyDetection
| project Temperature, Humidity, CO2, Occupancy
| where rand() < 0.1
| evaluate python(typeof(plotly:string),
```if 1:
    import plotly.express as px
    fig = px.scatter_3d(df, x='Temperature', y='Humidity', z='CO2', color='Occupancy')
    fig.update_layout(title=dict(text="Occupancy detection, plotly 5.11.0"))
    plotly_obj = fig.to_json()
    result = pd.DataFrame(data = [plotly_obj], columns = ["plotly"])
```)
~~~

:::image type="content" source="media/dashboard-customize-visuals/plotly-scatter3d.png" alt-text="Screenshot of plotly visual type.":::

> [!NOTE]
> For best performance, make sure that the python plugin image contains the latest versions of both the Python engine (currently 3.10.8) and Plotly package (currently 5.11.0). These versions can be checked with the [get_packages_version_fl()](kusto/functions-library/get-packages-version-fl.md) function. To upgrade the Python image see [Change the Python language extensions image on your cluster](language-extensions.md#change-the-python-language-extensions-image-on-your-cluster).
Plotly visuals are useful for advanced charting including geographic, scientific, machine learning, 3d, animation and many other chart types. For more information, see [Plotly](https://plotly.com/python/).
